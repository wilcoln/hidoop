grammar fr.n7.tdg.TDG with org.eclipse.xtext.common.Terminals

generate tDG "http://www.n7.fr/tdg/TDG"

Jeu:
	'jeu' name=ID '{'
		elements+=Element+
	 	parties+=Partie+
	 '}';

Partie:
	'partie' '{'
		terrain=Terrain
		niveaux+=Niveau+
		vagues+=Vague+
	'}';

Vague:
	'vague'
	 energieJoueur=INT 
	 obstaclesVague+=ObstacleVague*
	 mobilesVague+=MobileVague*
;

MobileVague:
	'mv' mobile=[Mobile] '{' 
		position=INT
		'entre en' ligneE=INT colonneE=INT
		'sort en' ligneS=INT colonneS=INT
	'}';
ObstacleVague:
	'ov' obstacle=[Obstacle] 'est en' ligne=INT colonne=INT
;
	
Niveau:
	'niveau' 
	name=ID 
	dureePause=INT 
	energieInitiale=INT 
	toleranceMobiles=INT
;

Terrain:
	'terrain' '{'
		chemins+=Chemin*
		decorateurs+=Decorateur*
		entrees+=Entree*
		sorties+=Sortie*
		lignes+=Ligne+
	'}'
;
Ligne:
	'ligne' cases+=Case+;

Case:
	 chemin=[Chemin]|decorateur=[Decorateur]|entree=[Entree]|sortie=[Sortie]
;
Chemin:
	'chemin'
	name=ID
	energie=INT
	volume=INT
;

Decorateur:
	'decorateur'
	name=ID
;

Entree:
	'entree'
	name=ID
;

Sortie:
	'sortie'
	name=ID
;

Element:
	'element' 
	|Mobile
	|Obstacle
	|Projectile
;

Projectile:
	'projectile' name=ID 
	 masse=INT
	 vitesse=INT 
	 portee=INT 
	 energie=INT
;

Obstacle:
	'obstacle' name=ID  volume=INT  energieMax=INT energieDispo=INT
;

Mobile:
	'mobile' name=ID 
	volume=INT 
	energieMax=INT 
	energieDispo=INT 
	vitesse=INT 
	tactique=TactiqueType
	projectiles+=[Projectile]*
;

enum TactiqueType :
	attaquePlusFaible = 'faible'
	|attaquePlusProche = 'proche'
	|attaquePlusFort = 'fort'
;
	
	
